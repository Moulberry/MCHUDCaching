buildscript {
    repositories {
        jcenter()
        maven {
            name 'JitPack'
            url 'https://jitpack.io'
        }
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:86b2392"
        classpath 'com.github.LxGaming:MixinGradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "1.0-REL"
group= "io.github.moulberry"
archivesBaseName = "HUDCaching"
String modid = "hudcaching"
String mixinClassifier = "dep"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
}

repositories {
    jcenter()
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}

mixin {
    add sourceSets.main, "mixins.${modid}.refmap.json"
}

jar {
    manifest.attributes(
		'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
		'MixinConfigs': "mixins.${modid}.json",
		'FMLCorePluginContainsFMLMod': true,
		"ForceLoadAsMod": true,
        "FMLAT": 'hudcaching_at.cfg'
    )
}

shadowJar {
	dependencies {
		include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
    }

	exclude 'module-info.class'
    exclude 'dummyThing'
    exclude 'LICENSE.txt'

    classifier = mixinClassifier
}

reobf {
	shadowJar {
		mappingType = 'SEARGE'
	}
}

runClient {
	standardInput = System.in
}

build.dependsOn(shadowJar)
runClient.dependsOn(build)

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}
moveResources.dependsOn processResources
classes.dependsOn moveResources
